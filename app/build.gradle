def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0
def versionCode_trans = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild

apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"


    //standardní pojmenování apk souboru nic moc nevypovídá o daném buildu.
    //tímto způsobem snadno rozlišíte mezi jednotlivými buildy a verzemi
    applicationVariants.all { variant -> // .all stejně jako each prochází všechny prvky kolekce, ale aplikuje se i v případě, že v ní přibudou nové prvky
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "$project.name-$variant.name-$variant.versionName-${variant.versionCode}.apk")
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    defaultConfig {
        applicationId "cz.muni.fi.pv256.movio2.uco_396546_themoviedb"
        minSdkVersion 17
        targetSdkVersion 24
        resConfigs "cs", "en"
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug{
            versionNameSuffix ".${versionBuild}.${versionCode_trans}.debug"
        }
    }
    productFlavors {
        first {
            //resValue "string", "app_name", "uco_396546_themoviedb"
            buildConfigField "boolean", "LOGING", "false"
        }
        second {
            //resValue "string", "app_name", "uco_396546_themoviedb second"
            //applicationId "cz.muni.fi.pv256.movio2.uco_396546_themoviedb_second_flavour"
            //applicationId 'cz.muni.fi.pv256.movio2.uco_396546_themoviedb_second'
            buildConfigField "boolean", "LOGING", "true"
            applicationId 'cz.muni.fi.pv256.movio1.comletly_different'
        }
    }
    lintOptions {
        //checkReleaseBuilds false
        //abortOnError false
        textReport true // když je true, vygeneruje se textový report problémů (default je false)
        textOutput 'stdout' // kam zapsat výstup. buď do souboru - file nebo do 'stdout' (standardní výstup)
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support'
        //knihovna v sobe sama pouziva support library, ktera muze byt v konfliktu s verzi support library vaseho projektu, proto ji odstranime
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha8'
    compile 'com.android.support:design:24.2.1'
}
